package dyvilx.tools.dpf

import dyvilx.tools.dpf.ast._
import dyvilx.tools.dpf.ast.builder._
import dyvilx.tools.dpf.ast.value._

public class DPFSyntax
{
	/**
	 * Root Node
	 *
	 * dpf { ... }
	 */
	static func dpf(configure: Configure<RootNode>) -> RootNode = new RootNode().use(configure)

	/**
	 * Node creation
	 *
	 * "name" { ... }
	 */
	infix func apply(name: Name, configure: Configure<Node>) -> Node = new Node(name).use(configure)

	/**
	 * Nested Node creation
	 *
	 * "parent" {
	 *     "child" { ... }
	 * }
	 */
	infix func applyStatement(base: Node, node: Node) -> void = base.add(node)

	/**
	 * Property creation with Colon
	 *
	 * "name": value
	 */
	infix func applyStatement(base: Node, pair: (Name, Value)) -> void = base.add(new Property(pair._1, pair._2))

	/**
	 * Builder creation
	 *
	 * "Name"("value", "label": "value")
	 */
	infix func apply(name: Name, parameters: (Value | (Name, Value))...) -> Builder
	{
		let builder = new Builder(name)

		for (element <- parameters) element match
		{
			case let v: Value => builder.add(Parameter(v))
			case (let n: Name, let v: Value) => builder.add(Parameter(n, v))
		}

		return builder
	}

	/**
	 * Builder with node
	 *
	 * "Name"("value", "label": "value") { ... }
	 */
	infix func apply(name: Name, parameters: (Value | (Name, Value))..., node configure: Configure<Node>) -> Builder
	{
		let builder = name(parameters...)
		configure(builder.visitNode())
		return builder
	}
}
